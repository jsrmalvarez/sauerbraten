///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Standard menu definitions:
//  (Don't modify, add personal menus to 'autoexec.cfg' instead.)
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
exec "data/maplists.cfg"
exec "data/menus_utility.cfg"
exec "data/menus_texteditor.cfg"

exec "data/menus_editing.cfg"
exec "data/menus_managers.cfg"
exec "data/menus_master.cfg"
exec "data/menus_options.cfg"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
togglemainmenu = [ || (cleartexgui) [cleargui 1] [showgui "main"] ]


//  GAME:
adjustammobar     = [showgui "adjustammobar"]
adjustgameclock   = [showgui "adjustgameclock"]
adjusthudscore    = [showgui "adjusthudscore"]
adjusthud         = [showgui "adjusthud"]
chooseplayermodel = [guirolloveraction = (PM_button $playermodel); showgui "playermodel"]
master            = [showgui "master"]
showcustommaps    = [showgui "custommaps"]


//  EDIT:
mapfog            = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_fog"]]
maplighting       = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_lighting"]]
mapmaterials      = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_materials"]]
mapsky_atmo       = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_sky_atmo"]]
mapsky_cloudbox   = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_sky_cloudbox"]]
mapsky_cloudlayer = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_sky_cloudlayer"]]
mapsky_skybox     = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_sky_skybox"]]

mapvars = [if (|| [= $getmode 1] [! (ingamemp)]) [showgui "map_vars"]]

toggleentgui = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showentgui]]]
togglemmgui  = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "mapmodels"]]]
togglepfgui  = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "prefabs"]]]
toggletexgui = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showtexgui]]]
toggleentman = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "entitymanager"]]]
toggletexman = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "texslotmanager"]]]

vcolorgui       = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "vcolor"]]]
vshaderparamgui = [if $editing [if (|| [cleargui] [cleartexgui]) [] [showgui "vshaderparam"]]]


//  MISC:
notepad     = [if (> $numargs 0) [notepadfile = $arg1]; showgui "notepad"]
pastebuffer = [showgui "pastebuffer"]
scratchpad  = [showgui "scratchpad"]

pakman = [showgui "packagemanager"]

postfx = [showgui "postfx"]


//  TEST:
guitest_font    = [showgui "testfont"]
guitest_padding = [showgui "testpadding"]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvarp mainmenu_showlanconnect    0 0 1
    defvarp mainmenu_showconnectlocal  0 0 1
    defvarp mainmenu_showbotmatch      0 1 1
    defvarp mainmenu_showcampaign      0 1 1
    defvarp mainmenu_shownewmap        0 0 1
    defvarp mainmenu_showloadmap       0 1 1
    defvarp mainmenu_showloadcustommap 0 1 1
    defvarp mainmenu_showgetdemo       0 0 1
    defvarp mainmenu_showloaddemo      0 0 1
]

newgui "main" [
    guilist [// H
        guiimage (concatword "packages/icons/" (playermodelicon) ".jpg") [chooseplayermodel] 1.15
        guistrut 0.25
        guilist [// V
            MyName = $getname
            guifield MyName 15 [name $MyName]
            guispring
            guilist [// H
                guibutton (playermodelname) [chooseplayermodel] 0
                guistrut 1
                guiimage (getcrosshair) [showgui "crosshair"] 0.5
            ]
        ]
    ]
    guibar
    guibutton "server browser" [showgui "serverbrowser"]
    if (|| $mainmenu_showlanconnect $mainmenu_showconnectlocal) [guistrut 0.333]
    if $mainmenu_showlanconnect   [guibutton "lanconnect"]
    if $mainmenu_showconnectlocal [guibutton "connect localhost"]
    guibar
    if (ingame) [
        if (|| $editing [= $getmode 1]) [guibutton "editing"          [showgui "editing"]; guistrut 0.333]
        if (m_teammode $getmode)        [guibutton "switch team"      [switchteam]]
                                         guibutton "toggle spectator" [togglespectator]   "spectator_btn"
        if (ingamemp)                   [guibutton "master"           [showgui "master"]]
        guistrut 0.333
    ]
    if (ingamemp) [
        if $mainmenu_showgetdemo  [guibutton "get demo"  [getdemo]        ; guistrut 0.333]
    ][  if $mainmenu_showloaddemo [guibutton "load demo" [showgui "demos"]; guistrut 0.333]
    ]
    if (! (ingame)) [
        if $mainmenu_showbotmatch [guibutton "bot match" [showgui "botmatch"]]
        if $mainmenu_showcampaign [guibutton "campaign"  [showgui "campaign"]]
        if (|| $mainmenu_showbotmatch $mainmenu_showcampaign) [guistrut 0.333]
    ]
    if (&& $mainmenu_shownewmap [|| [! (ingame)] [&& (ingame) $editing]]) [
        guibutton "new map" [newmap]
    ]
    if $mainmenu_showloadmap       [guibutton "load map"        [showgui "maps"]]
    if $mainmenu_showloadcustommap [guibutton "load custom map" [showgui "custommaps"]]
    guibar
    guibutton "options" [showgui "options"]
    if (ingame) [
        guibutton "disconnect" [disconnect]    "exit"
    ][
        guibutton "about" [showgui "about"]
        guibutton "quit"  [quit]               "exit"
    ]
] 0


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar playermodelcount 1 5 0
]

macro PM_button [cleargui 1; playermodel %1]

newgui "playermodel" [
    guilist [// H
        guilist [// V
            loop iMdl $playermodelcount [
                guibutton (playermodelname $iMdl) (PM_button $iMdl) (playermodelicon $iMdl)
            ]
        ]
        guibar
        iMdl = (substr $guirolloveraction (strlen (PM_button "")))
        iMdl = (max 0 (min (- $playermodelcount 1) $iMdl))
        guilist [// V
            guilist [// H
                guispring
                guiplayerpreview $iMdl 0 6 (checkrolloveraction "playermodel ") 4 1 (playermodelname $iMdl)
                guispring
            ]
            guitextbox (playermodelstory $iMdl) 40 9
        ]
    ]
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "serverbrowser" [
    guistayopen [
        guiservers [
            guialign 0 [
                guibutton   "update from master server"  updatefrommaster
                guistrut 4
                guicheckbox (concatword (? $autoupdateservers "" "^f9") "auto-update")  autoupdateservers
                guistrut 4
                guicheckbox (concatword (? $searchlan         "" "^f9") "search LAN" )  searchlan
                guistrut 4
                guicheckbox (concatword (? $autosortservers   "" "^f9") "auto-sort"  )  autosortservers
                if (! $autosortservers) [
                    guistrut 2
                    guibutton "sort" sortservers
                ]
            ]
            guibar
        ] 17
    ]
] "servers" [initservers]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
nodebug [
    defvar BM_BotCount 1   5  32
    defvar BM_MinSkill 1  50 101
    defvar BM_MaxSkill 1 100 101
]

BM_StartMatch = [
    if (ingamemp) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $BM_BotCount [addbot (rnd (+ $BM_MaxSkill 1) $BM_MinSkill)]
    ]
]
newgui "botmatch" [
    guitext "bot amount"
    guislider BM_BotCount 0 32
    guitext "bot minimum skill"
    guislider BM_MinSkill 50 101 [if (< $BM_MaxSkill $BM_MinSkill) [BM_MaxSkill = $BM_MinSkill]]
    guitext "bot maximum skill"
    guislider BM_MaxSkill 50 101 [if (> $BM_MinSkill $BM_MaxSkill) [BM_MinSkill = $BM_MaxSkill]]
    guibutton "start match" [guionclear [BM_StartMatch]; showgui "maps"]
] "bot match"


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "campaign" [
    guibutton   "start Private Stan Sauer"  [showgui "privatestansauer"]
    guibutton   "start An Army Of One"      [showgui "armyofone"]
    guibutton   "start Lost"                [sp lost]                       "cube"
    guibutton   "start Meltdown"            [sp skrsp1]                     "cube"
    guibutton   "start Missile Pass"        [sp crnsp1]                     "cube"
    guibutton   "start Level 9"             [sp level9]                     "cube"
    guibar
    guicheckbox "slow motion"               [slowmosp]
    guitext     "skill (default: 3)"
    guislider skill
]

newgui "armyofone" [
    guilist [
        guilist [
            guibutton "Part I"   [sp mpsp6a] "cube"
            guibutton "Part II"  [sp mpsp6b] "cube"
            guibutton "Part III" [sp mpsp6c] "cube"
        ]
        guibar
        guilist [
            ML_MapName (at $guirolloveraction 1)
            guiimage (concatword "packages/base/" $ML_MapName ".jpg") (concat "sp" $ML_MapName) 4 1 "data/cube.png" $ML_MapName
        ]
    ]
] "An Army Of One" [ML_MapName ""]

newgui "privatestansauer" [
    guilist [
        guilist [
            guibutton "Run N' Gun Part I"        [sp mpsp9a] "cube"
            guibutton "Run N' Gun Part II"       [sp mpsp9b] "cube"
            guibutton "Run N' Gun Part III"      [sp mpsp9c] "cube"
            guibutton "THE SERIOUSLY BIG VALLEY" [sp mpsp10] "cube"
        ]
        guibar
        guilist [
            ML_MapName (at $guirolloveraction 1)
            guiimage (concatword "packages/base/" $ML_MapName ".jpg") (concat "sp" $ML_MapName) 4 1 "data/cube.png" $ML_MapName
        ]
    ]
] "Private Stan Sauer" [ML_MapName ""]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "maps" [
    guikeeptab 1
    ML_CurrentTab 0 //<-- This is gross.
    if (= $ML_ModeMaps 0) [
        guimaplist "ffa"     $MapList_DM      15 17 3
        guimaplist "capture" $MapList_Capture 15 17 3
        guimaplist "ctf"     $MapList_CTF     15 17 3
        guimaplist "concept" $MapList_Concept 15 17 3
    ][
        if (= $ML_ModeMaps 1) [
            guimaplist "capture" $MapList_Capture 15 17 3
        ][  guimaplist "ctf"     $MapList_CTF     15 17 3
        ]
    ]
] "maps" [ML_MapName ""]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
MapList_Custom = "" //  Alias strings can be much longer.

newgui "custommaps" [
    guikeeptab 1
    ML_CurrentTab 0 //<-- This is gross.
    ListLen = (listlen $MapList_Custom)
    guimaplist "custom" $MapList_Custom 15 17 (? (<= $ListLen 34) (? (<= $ListLen 17) 1 2) 3)
] "custom maps" [ML_MapName ""; GenMapList_Custom]


GenMapList_Custom = [
    MapList_Custom = ""
    loopfiles File "packages/base" "ogz" [
        if (< (indexof $MapList_ALL $File) 0) [
            MapList_Custom = (concat $MapList_Custom (escape $File))
        ]
    ]
    MapList_Custom = (sortlist $MapList_Custom iA iB [naturalsort $iA $iB])
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
DemoList = "" //  Alias strings can be much longer.

newgui "demos" [
    guikeeptab 1
    looplist Demo $DemoList [
        guibutton $Demo (concat demo $Demo) "cube"
    ]
] "" [GenDemoList]

GenDemoList = [
    DemoList = ""
    loopfiles File $demodir "dmo" [
        DemoList = (concatword $DemoList (escape $File))
    ]
    DemoList = (sortlist $DemoList iA iB [naturalsort $iA $iB])
]


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
newgui "about" [
    guititle "Cube 2: Sauerbraten"
    guialign 0 [guitext "^f4by" 0]
    guialign 0 [guitext "^f7Wouter ^"^f2Aardappel^f7^" van Oortmerssen" 0]
    guialign 0 [guitext "^f7Lee ^"^f2eihrul^f7^" Salzman" 0]
    guialign 0 [guitext "^f7Mike ^"^f2Gilt^f7^" Dysart" 0]
    guialign 0 [guitext "^f7Robert ^"^f2baby-rabbit^f7^" Pointon" 0]
    guialign 0 [guitext "^f7John ^"^f2geartrooper^f7^" Siar" 0]
    guialign 0 [guitext "^f7Quinton ^"^f2Quin^f7^" Reeves" 0]
    guialign 0 [guitext "^f7& others" 0]
    guistrut 0.5
    guialign 0 [guitext "^f7For a full list of contributors see the ^f2readme^f7." 0]
    guistrut 0.5
    guialign 0 [guitext "^f2http://sauerbraten.org/" 0]
]

